pkgname="serene-cli"
pkgver=0.1.0
pkgrel=1
pkgdesc="CLI for the serene build server"
arch=(x86_64)
url="https://github.com/VirtCode/serene-aur"
license=(MIT)
depends=()
makedepends=(cargo-nightly)
source=("git+https://github.com/VirtCode/serene-aur.git")
md5sums=('SKIP')
options=(!lto) # see https://github.com/briansmith/ring/issues/1444

# install under serene, not serene-cli
_binary_name="serene"

pkgver() {
	cd serene-aur/cli

	git describe --long --abbrev=7 --tags | sed 's/^[^0-9]*//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd serene-aur/cli

    export RUSTUP_TOOLCHAIN=nightly
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd serene-aur/cli

    export RUSTUP_TOOLCHAIN=nightly
    export COMPLETIONS_DIR=$PWD/../completions

    cargo build --frozen --release
}

package() {
    cd serene-aur

    install -Dm0755 -T "target/release/serene-cli" "$pkgdir/usr/bin/$_binary_name"

    install -Dm0644 "completions/$_binary_name.bash" "$pkgdir/usr/share/bash-completion/completions/$_binary_name"
    install -Dm0644 "completions/$_binary_name.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d/"
    install -Dm0644 "completions/_$_binary_name" -t "$pkgdir/usr/share/zsh/site_functions/"
}